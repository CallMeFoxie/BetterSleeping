def VersionMajor = 1
def VersionMinor = 3

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

def VersionBuild = 0
if (System.getenv("BUILD_NUMBER") != null)
    VersionBuild = System.getenv("BUILD_NUMBER")

version = "1.7.10-${VersionMajor}.${VersionMinor}.${VersionBuild}"
group = "cz.ondraster.bettersleeping"
archivesBaseName = "BetterSleeping"

minecraft {
    version = "1.7.10-10.13.4.1470-1.7.10"
    runDir = "run"
    replace '@VERSION@', project.version
}

repositories {
    maven {
        name "OpenMods"
        url "http://repo.openmods.info/artifactory/openmods"
    }
}

dependencies {
    compile(group: 'openblocks', name: 'OpenBlocks-API', version: '1.0')
}

sourceSets {
    bsapi {
        java {
            srcDir 'src/main/java/cz/ondraster/bettersleeping/api'
        }
    }
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version': project.version, 'mcversion': project.minecraft.version
            }
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }
jar {
    manifest {
        attributes 'FMLCorePlugin': 'foxie.bettersleeping.asm.BetterSleepingCoreLoader'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from(sourceSets.main.java) {
        include "cz/ondraster/bettersleeping/api/"
    }
    classifier = 'api'
}

artifacts {
    archives deobfJar
    archives apiJar
}

build.dependsOn deobfJar, apiJar